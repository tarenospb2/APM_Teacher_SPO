 // Получает список студентов группы, отсортированный по алфавиту
//
// Параметры:
//  Группа  - СправочникСсылка.Группы - содержит ссылку на группу
//  ДатаЗапроса  - Дата - дата, на которую студент активен
//
// Возвращаемое значение:
//   Массив[2]   - Содержит массив ссылок на всех студентов (СправочникСсылка.Студенты) и массив ссылок на отчисленных (0-1)
//
Функция ПолучитьВыборкуСтудентовГруппы(Группа, ДатаЗапроса) Экспорт
	
	Мас = Новый Массив;
	МассивСсылок = Новый Массив;
	МассивОтчисленных = Новый Массив; // используется при выводе в журнале!
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Студенты.Наименование КАК Наименование,
		|	Студенты.Отчислен КАК Отчислен,
		|	Студенты.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Студенты КАК Студенты
		|ГДЕ
		|	Студенты.Группа = &Группа
		|	И Студенты.УдаленИзГруппы >= &ДатаЗапроса
		|	И Студенты.ПринятВГруппу <= &ДатаЗапроса
		|
		|УПОРЯДОЧИТЬ ПО
		|	Наименование";
	
	Запрос.УстановитьПараметр("Группа", Группа);
	Запрос.УстановитьПараметр("ДатаЗапроса", ДатаЗапроса);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ДетальныеЗапросы = РезультатЗапроса.Выбрать();
	
	Пока ДетальныеЗапросы.Следующий() Цикл
		
		МассивСсылок.Добавить(ДетальныеЗапросы.Ссылка);
		Если ДетальныеЗапросы.Отчислен Тогда
		
			МассивОтчисленных.Добавить(1);	
		
		Иначе
		
			МассивОтчисленных.Добавить(0);
		
		КонецЕсли;
		
	
	КонецЦикла;
	
	Мас.Добавить(МассивСсылок);
	Мас.Добавить(МассивОтчисленных);
	
	Возврат Мас;
	 
КонецФункции // ПолучитьВыборкуСтудентовГруппы() 


 // Получает количество неотчисленных студентов группы
//
// Параметры:
//  Группа  - СправочникСсылка.Группы - содержит ссылку на группу
//  ДатаЗапроса  - Дата - дата, на которую студент активен
//
// Возвращаемое значение:
//   Число   - Количество неотчисленных студентов
//
Функция ПолучитьКоличествоСтудентовГруппы(Группа, ДатаЗапроса) Экспорт
	
	Мас = Новый Массив;
	МассивСсылок = Новый Массив;
	МассивОтчисленных = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Студенты.Наименование) КАК Колво
		|ИЗ
		|	Справочник.Студенты КАК Студенты
		|ГДЕ
		|	Студенты.Группа = &Группа
		|	И Студенты.Отчислен = ЛОЖЬ
		|	И Студенты.ПринятВГруппу <= &ДатаЗапроса
		|	И Студенты.УдаленИзГруппы >= &ДатаЗапроса";
	
	Запрос.УстановитьПараметр("Группа", Группа);
	Запрос.УстановитьПараметр("ДатаЗапроса", ДатаЗапроса);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ДетальныеЗапросы = РезультатЗапроса.Выбрать();
	
	Пока ДетальныеЗапросы.Следующий() Цикл
		
		Возврат ДетальныеЗапросы.Колво;		
	
	КонецЦикла;
	
	Возврат 0;
	 
КонецФункции // ПолучитьКоличествоСтудентовГруппы()

 // Получает количество проведенных занятий за текущий месяц
//
// Параметры:
//  Предмет  - СправочникСсылка.Предметы - содержит ссылку на предмет
//  Группа  - СправочникСсылка.Группы - содержит ссылку на группу
//  Дата  - Дата - содержит требуемую дату в рамках месяца
//
// Возвращаемое значение:
//   Число   - Количество проведенных занятий
//
Функция ПолучитьКоличествоПроведенныхЗанятий(Предмет, Группа, Дата) Экспорт
	
	УчебныйГод = РаботаССеансом.ПолучитьНачалоИКонецУчебногоГода();
	
   	//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	Колво = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(Расписание.НомерПары) КАК КоличествоПар
		|ИЗ
		|	РегистрСведений.Расписание КАК Расписание
		|ГДЕ
		|	Расписание.ЗанятиеПроведено = ИСТИНА
		|	И Расписание.Предмет = &Предмет
		|	И Расписание.Группа = &Группа
		|	И Расписание.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Расписание.Преподаватель = &Преподаватель
		|	И Расписание.ПериодУчебы = &ПериодУчебы";
	
	Запрос.УстановитьПараметр("Группа", Группа);
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("Предмет", Предмет);
	Запрос.УстановитьПараметр("Преподаватель", ПараметрыСеанса.ТекПользователь);
	Запрос.УстановитьПараметр("ПериодУчебы", УчебныйГод.Период);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Колво = ВыборкаДетальныеЗаписи.КоличествоПар;
	КонецЦикла;
	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
   Возврат Колво;

КонецФункции // ПолучитьКоличествоПроведенныхЗанятий()

 // Получает количество проведенных занятий за любой интервал времени
//
// Параметры:
//  Предмет  - СправочникСсылка.Предметы - содержит ссылку на предмет
//  Группа  - СправочникСсылка.Группы - содержит ссылку на группу
//  ДатаН  - Дата - содержит начальную дату для формирования
//  ДатаК  - Дата - содержит конечную дату для формирования
//
// Возвращаемое значение:
//   Число   - Количество проведенных занятий                                     
//
Функция ПолучитьКоличествоПроведенныхЗанятийВнеплановая(Предмет, Группа, ДатаН, ДатаК) Экспорт
	
	УчебныйГод = РаботаССеансом.ПолучитьНачалоИКонецУчебногоГода();
	
   	//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	Колво = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(Расписание.НомерПары) КАК КоличествоПар
		|ИЗ
		|	РегистрСведений.Расписание КАК Расписание
		|ГДЕ
		|	Расписание.ЗанятиеПроведено = ИСТИНА
		|	И Расписание.Предмет = &Предмет
		|	И Расписание.Группа = &Группа
		|	И Расписание.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Расписание.Преподаватель = &Преподаватель
		|	И Расписание.ПериодУчебы = &ПериодУчебы";
	
	Запрос.УстановитьПараметр("Группа", Группа);
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(ДатаК));
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(ДатаН));
	Запрос.УстановитьПараметр("Предмет", Предмет);
	Запрос.УстановитьПараметр("Преподаватель", ПараметрыСеанса.ТекПользователь);
	Запрос.УстановитьПараметр("ПериодУчебы", УчебныйГод.Период);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Колво = ВыборкаДетальныеЗаписи.КоличествоПар;
	КонецЦикла;
	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
   Возврат Колво;

КонецФункции // ПолучитьКоличествоПроведенныхЗанятийВнеплановая()

 // Получает последнюю аттестацию студентов по предмету
//
// Параметры:
//  МассивСтудентов  - Массив - содержит массив ссылок студентов
//  Предмет  - СправочникСсылка.Предметы - содержит ссылку на предмет
//
// Возвращаемое значение:
//   Массив   - Содержит структуры (Студент - СправочникСсылка.Студенты; Аттестация - Число)                                     
//
Функция ПолучитьПоследнююАттестацию(МассивСтудентов, Предмет, Дата) Экспорт
	
	Мас = Новый Массив;
	//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АттестацияОбороты.Студент КАК Студент,
		|	АттестацияОбороты.ТипАттестации КАК ТипАттестации,
		|	АттестацияОбороты.БаллОборот КАК БаллОборот
		|ИЗ
		|	РегистрНакопления.Аттестация.Обороты(
		|			,
		|			&Кон,
		|			Запись,
		|			Преподаватель = &Преподаватель
		|				И Студент В (&МасСтуд)
		|				И Предмет = &Предмет) КАК АттестацияОбороты
		|ИТОГИ ПО
		|	Студент";
	
	 Запрос.УстановитьПараметр("МасСтуд", МассивСтудентов);
	  Запрос.УстановитьПараметр("Кон", Дата);
	Запрос.УстановитьПараметр("Предмет", Предмет);
	Запрос.УстановитьПараметр("Преподаватель", ПараметрыСеанса.ТекПользователь);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаСтудент = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаСтудент.Следующий() Цикл
		// Вставить обработку выборки ВыборкаСтудент
	
		ВыборкаДетальныеЗаписи = ВыборкаСтудент.Выбрать();
	    СтруктураЗаписи = Новый Структура;
		СтруктураЗаписи.Вставить("Студент", ВыборкаСтудент.Студент);
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Если ВыборкаДетальныеЗаписи.ТипАттестации = Перечисления.ТипАттестации.Исправление Тогда
			
				СтруктураЗаписи.Вставить("Аттестация", ВыборкаДетальныеЗаписи.БаллОборот); 
			    Прервать;
			Иначе
				СтруктураЗаписи.Вставить("Аттестация", ВыборкаДетальныеЗаписи.БаллОборот);
			КонецЕсли;
			
			
			
		КонецЦикла;
		Мас.Добавить(СтруктураЗаписи);
	КонецЦикла;
	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА

    Возврат Мас;
    

КонецФункции // ПолучитьПоследнююАттестацию()


 // Получает статистику занятий студентов за текущий месяц
//
// Параметры:
//  Предмет  - СправочникСсылка.Предметы - содержит ссылку на предмет
//  Группа  - СправочникСсылка.Группы - содержит ссылку на группу
//  Дата  - Дата - содержит требуемую дату в рамках месяца
//  ВыполнятьОтборПодгруппы  - Булево - содержит сведения об отборе студентов своей подгруппы
//
// Возвращаемое значение:
//   Массив   - Содержит структуры (Студент - СправочникСсылка.Студенты; КоличествоОценок,СреднийБалл,КоличествоПропусков - Число)                                     
//
Функция ПолучитьСтатистикуДляАттестации(Предмет, Группа, Дата1, Дата2, ВыполнятьОтборПодгруппы) Экспорт
	
	УчебныйГод = РаботаССеансом.ПолучитьНачалоИКонецУчебногоГода();
	МассивСтудентов = Новый Массив;
	Если Не ВыполнятьОтборПодгруппы Тогда
		
		СтудентыДляОтбора = ПолучитьВыборкуСтудентовГруппы(Группа,Дата2)[0];
	Иначе
		
		СтудентыДляОтбора = ВыбратьСтудентовСвоейПодгруппы(Группа, Предмет, Дата2);
		
	КонецЕсли;
	ПоследняяАттестация = ПолучитьПоследнююАттестацию(СтудентыДляОтбора,Предмет, Дата2-1);
	
	//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Студенты.Ссылка КАК Ссылка,
		|	Студенты.Наименование КАК Наименование
		|ПОМЕСТИТЬ ВсеСтуденты
		|ИЗ
		|	Справочник.Студенты КАК Студенты
		|ГДЕ
		|	Студенты.Отчислен = ЛОЖЬ
		|	И Студенты.Ссылка В(&МасСтуд)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВсеСтуденты.Ссылка КАК Студент,
		|	ЗанятияОбороты.ТипБалла КАК ТипБалла,
		|	ЕСТЬNULL(ЗанятияОбороты.БаллОборот, 0) КАК БаллОборот
		|ИЗ
		|	ВсеСтуденты КАК ВсеСтуденты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Занятия.Обороты(
		|				&НачПериода,
		|				&КонПериода,
		|				Запись,
		|				Предмет = &Предмет
		|					И Студент В
		|						(ВЫБРАТЬ
		|							ВсеСтуденты.Ссылка КАК Ссылка
		|						ИЗ
		|							ВсеСтуденты КАК ВсеСтуденты)
		|					И Преподаватель = &Преподаватель
		|					И ПериодУчебы = &ПериодУчебы) КАК ЗанятияОбороты
		|		ПО ВсеСтуденты.Ссылка = ЗанятияОбороты.Студент
		|ИТОГИ ПО
		|	Студент";
	
	Запрос.УстановитьПараметр("Группа", Группа);
	Запрос.УстановитьПараметр("КонПериода", Дата2);
	Запрос.УстановитьПараметр("НачПериода", Дата1);
	Запрос.УстановитьПараметр("Предмет", Предмет);
	Запрос.УстановитьПараметр("Преподаватель", ПараметрыСеанса.ТекПользователь);
	Запрос.УстановитьПараметр("ПериодУчебы", УчебныйГод.Период);
	Запрос.УстановитьПараметр("МасСтуд", СтудентыДляОтбора);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаСтудент = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаСтудент.Следующий() Цикл
		// Вставить обработку выборки ВыборкаСтудент
		
		Показатели = Новый Структура;
	    Показатели.Вставить("Студент",ВыборкаСтудент.Студент);
		КоличествоОценок = 0;
		ВсегоБаллов = 0;
		КоличествоПропусков = 0;
		КоличествоОценок345 = 0;
		КоличествоОценок2 = 0;
		
		ВыборкаДетальныеЗаписи = ВыборкаСтудент.Выбрать();
	
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			 Если ВыборкаДетальныеЗаписи.ТипБалла = Перечисления.ТипыБаллов.Оценка Тогда
				 Если ВыборкаДетальныеЗаписи.БаллОборот <> 2 Тогда
				 
				 	КоличествоОценок345 = КоличествоОценок345 + 1;  
				 
				 Иначе
					 
					 КоличествоОценок2 = КоличествоОценок2 + 1;
					
				 КонецЕсли;
				 
				 ВсегоБаллов = ВсегоБаллов + ВыборкаДетальныеЗаписи.БаллОборот;	
			 	  
			 Иначе
			 
			 	КоличествоПропусков = КоличествоПропусков + 1;
			 
			 КонецЕсли;
		 КонецЦикла;
		 
		Для каждого структ Из ПоследняяАттестация Цикл
		
			Если структ.Студент = ВыборкаСтудент.Студент Тогда
			
				ПрошлаяАттестацияСтудента = структ.Аттестация;
				Прервать;
			
			КонецЕсли;
		
		КонецЦикла; 
		
		КоличествоОценок = КоличествоОценок2 + КоличествоОценок345;
		Если КоличествоОценок2 < КоличествоОценок345 И ПрошлаяАттестацияСтудента <> 2 Тогда
			
			Показатели.Вставить("КоличествоОценок",КоличествоОценок);
			Показатели.Вставить("СреднийБалл",?(КоличествоОценок = 0,0,ВсегоБаллов/КоличествоОценок));
			Показатели.Вставить("КоличествоПропусков",КоличествоПропусков);
			
		Иначе
			Показатели.Вставить("КоличествоОценок",КоличествоОценок);
			Показатели.Вставить("СреднийБалл", 2);
			Показатели.Вставить("КоличествоПропусков",КоличествоПропусков);

		КонецЕсли;
				
		МассивСтудентов.Добавить(Показатели);
		
	КонецЦикла;
	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	
    Возврат МассивСтудентов;
	
КонецФункции // ПолучитьСтатистикуДляАттестации()

 // Определяет, является ли студент старостой
//
// Параметры:
//  Студент  - СправочникСсылка.Студенты - содержит ссылку на студента
//
// Возвращаемое значение:
//   Булево   - является Да или Нет студент старостой                                     
//
Функция ЯвляетсяСтаростой(Студент) Экспорт
	
	Возврат Студент.Староста; 

КонецФункции // ЯвляетсяСтаростой()

 // Получает индив настройки для пары Предмет-Группа
//
// Параметры:
//  Предмет  - СправочникСсылка.Предметы - содержит ссылку на предмет
//  Группа  - СправочникСсылка.Группы - содержит ссылку на группу
//
// Возвращаемое значение:
//   Массив[4]   - Содержит Ссылку на Мудл, Ссылку на Облако, Интервал с и по для удаления подгруппы                                     
//
Функция ПолучитьСсылкиНаРесурсы(Предмет, Группа) Экспорт
	
	УчебныйГод = РаботаССеансом.ПолучитьНачалоИКонецУчебногоГода();
	
		//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИндивидуальныеНастройкиГрупп.СсылкаНаОблако КАК СсылкаНаОблако,
		|	ИндивидуальныеНастройкиГрупп.СсылкаНаMoodle КАК СсылкаНаMoodle,
		|	ИндивидуальныеНастройкиГрупп.ИнтервалС КАК ИнтервалС,
		|	ИндивидуальныеНастройкиГрупп.ИнтервалПо КАК ИнтервалПо
		|ИЗ
		|	Справочник.ИндивидуальныеНастройкиГрупп КАК ИндивидуальныеНастройкиГрупп
		|ГДЕ
		|	ИндивидуальныеНастройкиГрупп.Предмет = &Предмет
		|	И ИндивидуальныеНастройкиГрупп.Группа = &Группа
		|	И ИндивидуальныеНастройкиГрупп.Преподаватель = &Преподаватель
		|	И ИндивидуальныеНастройкиГрупп.ПериодУчебы = &ПериодУчебы";
	
	Запрос.УстановитьПараметр("Группа", Группа);
	Запрос.УстановитьПараметр("Предмет", Предмет);
	Запрос.УстановитьПараметр("Преподаватель", ПараметрыСеанса.ТекПользователь);
	Запрос.УстановитьПараметр("ПериодУчебы", УчебныйГод.Период);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Мас = Новый Массив;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Мас.Добавить(ВыборкаДетальныеЗаписи.СсылкаНаMoodle);
		Мас.Добавить(ВыборкаДетальныеЗаписи.СсылкаНаОблако);
		Мас.Добавить(ВыборкаДетальныеЗаписи.ИнтервалС);
		Мас.Добавить(ВыборкаДетальныеЗаписи.ИнтервалПо);
	КонецЦикла;
	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА

	Возврат Мас;
	
	
	
КонецФункции // ПолучитьСсылкиНаРесурсы() 

 // Получает второго преподавателя, если он имеется
//
// Параметры:
//  Дата  - Дата - содержит текущую дату
//  НомерПары  - Число - содержит номер предмета в дне
//
// Возвращаемое значение:
//   СправочникСсылка.Преподаватели   - Содержит Ссылку на преподавателя                                     
//
Функция ПолучитьВторогоПреподавателя(Дата, НомерПары) Экспорт
	
	УчебныйГод = РаботаССеансом.ПолучитьНачалоИКонецУчебногоГода();
	 	//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	Препод = Справочники.Преподаватели.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасписаниеСрезПоследних.ВторойПреподаватель КАК ВторойПреподаватель
		|ИЗ
		|	РегистрСведений.Расписание.СрезПоследних(
		|			,
		|			Период = &Период
		|				И НомерПары = &НомерПары
		|				И Преподаватель = &Преподаватель
		|				И ПериодУчебы = &ПериодУчебы) КАК РасписаниеСрезПоследних";
	
	Запрос.УстановитьПараметр("НомерПары", НомерПары);
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Преподаватель", ПараметрыСеанса.ТекПользователь);
	Запрос.УстановитьПараметр("ПериодУчебы", УчебныйГод.Период);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Препод = ВыборкаДетальныеЗаписи.ВторойПреподаватель;
	КонецЦикла;
	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	
	Возврат Препод;

КонецФункции // ПолучитьВторогоПреподавателя() 

 // Получает регистратор для занятия
//
// Параметры:
//  ДатаЗанятия  - Дата - содержит текущую дату
//  Предмет  - СправочникСсылка.Предметы - содержит предмет
//  НомерПары  - Число - содержит номер занятия в дате
//  Группа  - СправочникСсылка.Группы - содержит группу
//
// Возвращаемое значение:
//   ДокументСсылка.ФормированиеДневногоРасписания   - Содержит Ссылку на элемент расписания                                     
//
Функция ПолучениеРегистратораРСПроведенияПары(ДатаЗанятия, Предмет, НомерПары, Группа) Экспорт
	
	УчГод = РаботаССеансом.ПолучитьНачалоИКонецУчебногоГода();
	
	 Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Расписание.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрСведений.Расписание КАК Расписание
		|ГДЕ
		|	Расписание.Период = &Период
		|	И Расписание.Предмет = &Предмет
		|	И Расписание.Группа = &Группа
		|	И Расписание.НомерПары = &НомерПары
		|	И Расписание.Преподаватель = &Преподаватель
		|	И Расписание.ПериодУчебы = &ПериодУчебы";
	
	Запрос.УстановитьПараметр("Группа", Группа);
	Запрос.УстановитьПараметр("Период", НачалоДня(ДатаЗанятия));
	Запрос.УстановитьПараметр("Предмет", Предмет);
	Запрос.УстановитьПараметр("НомерПары", НомерПары);
	Запрос.УстановитьПараметр("Преподаватель", ПараметрыСеанса.ТекПользователь);
	Запрос.УстановитьПараметр("ПериодУчебы", УчГод.Период);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		регистратор = ВыборкаДетальныеЗаписи.Регистратор;
	КонецЦикла;
	
	Возврат регистратор;

КонецФункции // ПолучениеРегистратораРСПроведенияПары()

 // Получает статистику посещения и успеваемости студентами одной группы в интервале времени
//
// Параметры:
//  Предмет  - СправочникСсылка.Предметы - содержит ссылку на предмет
//  Группа  - СправочникСсылка.Группы - содержит ссылку на группу
//  Дата1  - Дата - содержит начальную дату
//  Дата2  - Дата - содержит конечную дату
//  ВыполнятьОтборПодгруппы  - Булево - содержит сведения об отборе студентов своей подгруппы
//
// Возвращаемое значение:
//   Массив   - Содержит структуры (Дата - Дата; КоличествоПропусков,КоличествоОценок,КоличествоПятерок,КоличествоЧетверок,КоличествоТроек,КоличествоДвоек - Число)                                     
//
Функция ПолучитьСтатистикуПосещения(Предмет, Группа, Дата1, Дата2, ВыполнятьОтборПодгруппы) Экспорт
	
	УчебныйГод = РаботаССеансом.ПолучитьНачалоИКонецУчебногоГода();
	МассивЗанятий = Новый Массив;
	
	Если Не ВыполнятьОтборПодгруппы Тогда
		
		СтудентыДляОтбора = ПолучитьВыборкуСтудентовГруппы(Группа, Дата2)[0];
	Иначе
		
		СтудентыДляОтбора = ВыбратьСтудентовСвоейПодгруппы(Группа, Предмет, Дата2);
		
	КонецЕсли;
	
				    //{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
			// Данный фрагмент построен конструктором.
			// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ
				|	Студенты.Ссылка КАК Ссылка,
				|	Студенты.Наименование КАК Наименование
				|ПОМЕСТИТЬ ВсеСтуденты
				|ИЗ
				|	Справочник.Студенты КАК Студенты
				|ГДЕ
				|	Студенты.Отчислен = ЛОЖЬ
				|	И Студенты.Ссылка В(&МасСтуд)
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	ЗанятияОбороты.ТипБалла КАК ТипБалла,
				|	ЕСТЬNULL(ЗанятияОбороты.БаллОборот, 0) КАК БаллОборот,
				|	ЗанятияОбороты.Регистратор.ДатаЗанятия КАК РегистраторДатаЗанятия,
				|	ЗанятияОбороты.Регистратор.Дата КАК РегистраторДата
				|ИЗ
				|	РегистрНакопления.Занятия.Обороты(
				|			&НачПериода,
				|			&КонПериода,
				|			Запись,
				|			Предмет = &Предмет
				|				И Студент В
				|					(ВЫБРАТЬ
				|						ВсеСтуденты.Ссылка КАК Ссылка
				|					ИЗ
				|						ВсеСтуденты КАК ВсеСтуденты)
				|				И Преподаватель = &Преподаватель
				|				И ПериодУчебы = &ПериодУчебы) КАК ЗанятияОбороты
				|
				|УПОРЯДОЧИТЬ ПО
				|	РегистраторДатаЗанятия
				|ИТОГИ ПО
				|	РегистраторДата";
			
			Запрос.УстановитьПараметр("МасСтуд", СтудентыДляОтбора);
			Запрос.УстановитьПараметр("КонПериода", КонецДня(Дата2));
			Запрос.УстановитьПараметр("НачПериода", НачалоДня(Дата1));
			Запрос.УстановитьПараметр("Предмет", Предмет);
	        Запрос.УстановитьПараметр("Преподаватель", ПараметрыСеанса.ТекПользователь);
			Запрос.УстановитьПараметр("ПериодУчебы", УчебныйГод.Период);

	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДата = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаДата.Следующий() Цикл
		// Вставить обработку выборки ВыборкаДата
		
		КоличествоДвоек = 0;
		КоличествоТроек = 0;
		КоличествоЧетверок = 0;
		КоличествоПятерок = 0;
		КоличествоОценок = 0;
		КоличествоПропусков = 0;
		
		ВыборкаДетальныеЗаписи = ВыборкаДата.Выбрать();
	
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			
			 Если ВыборкаДетальныеЗаписи.ТипБалла = Перечисления.ТипыБаллов.Оценка Тогда
			 
				Если ВыборкаДетальныеЗаписи.БаллОборот = 2 Тогда
				
					КоличествоДвоек = КоличествоДвоек + 1;
				
				Иначе Если ВыборкаДетальныеЗаписи.БаллОборот = 3 Тогда
				
					КоличествоТроек = КоличествоТроек + 1;
					
				Иначе Если ВыборкаДетальныеЗаписи.БаллОборот = 4 Тогда
				
					КоличествоЧетверок = КоличествоЧетверок + 1;
					
				Иначе Если ВыборкаДетальныеЗаписи.БаллОборот = 5 Тогда
				
					КоличествоПятерок = КоличествоПятерок + 1;
					
				КонецЕсли;	
				КонецЕсли;	
				КонецЕсли;
				КонецЕсли; 
				
				КоличествоОценок = КоличествоОценок + 1;
				
			 Иначе
			 
			 	КоличествоПропусков = КоличествоПропусков + 1;
			 
			 КонецЕсли;
		КонецЦикла;
		
		Показатели = Новый Структура;
	    Показатели.Вставить("Дата",ВыборкаДетальныеЗаписи.РегистраторДата);
		Показатели.Вставить("КоличествоПропусков",КоличествоПропусков);
		Показатели.Вставить("КоличествоОценок",КоличествоОценок);
		Показатели.Вставить("КоличествоПятерок",КоличествоПятерок);
		Показатели.Вставить("КоличествоЧетверок",КоличествоЧетверок);
		Показатели.Вставить("КоличествоТроек",КоличествоТроек);
		Показатели.Вставить("КоличествоДвоек",КоличествоДвоек);
		
		МассивЗанятий.Добавить(Показатели);
		
	КонецЦикла;
	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	
    Возврат МассивЗанятий;
	
КонецФункции // ПолучитьСтатистикуПосещения() 

 // Получает статистику посещения и успеваемости студентами одной группы за конкретный день
//
// Параметры:
//  Предмет  - СправочникСсылка.Предметы - содержит ссылку на предмет
//  Группа  - СправочникСсылка.Группы - содержит ссылку на группу
//  Дата  - Дата - содержит дату
//  ВыполнятьОтборПодгруппы  - Булево - содержит сведения об отборе студентов своей подгруппы
//
// Возвращаемое значение:
//   Массив   - Содержит структуры (Дата - Дата; КоличествоПропусков,КоличествоОценок,КоличествоПятерок,КоличествоЧетверок,КоличествоТроек,КоличествоДвоек - Число)                                     
//
Функция ПолучитьСтатистикуПосещенияЗаОдинДень(Предмет, Группа, Дата, ВыполнятьОтборПодгруппы) Экспорт
	
	УчебныйГод = РаботаССеансом.ПолучитьНачалоИКонецУчебногоГода();
	МассивЗанятий = Новый Массив;
	
	Если Не ВыполнятьОтборПодгруппы Тогда
		
		СтудентыДляОтбора = ПолучитьВыборкуСтудентовГруппы(Группа, Дата)[0];
	Иначе
		
		СтудентыДляОтбора = ВыбратьСтудентовСвоейПодгруппы(Группа, Предмет, Дата);
		
	КонецЕсли;
	
				    //{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
			// Данный фрагмент построен конструктором.
			// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ
				|	Студенты.Ссылка КАК Ссылка,
				|	Студенты.Наименование КАК Наименование
				|ПОМЕСТИТЬ ВсеСтуденты
				|ИЗ
				|	Справочник.Студенты КАК Студенты
				|ГДЕ
				|	Студенты.Отчислен = ЛОЖЬ
				|	И Студенты.Ссылка В(&МасСтуд)
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	ЗанятияОбороты.ТипБалла КАК ТипБалла,
				|	ЕСТЬNULL(ЗанятияОбороты.БаллОборот, 0) КАК БаллОборот,
				|	ЗанятияОбороты.Регистратор.ДатаЗанятия КАК РегистраторДатаЗанятия,
				|	ЗанятияОбороты.Регистратор.Дата КАК РегистраторДата,
				|	ЗанятияОбороты.НомерПары КАК НомерПары,
				|	ЗанятияОбороты.Студент КАК Студент
				|ИЗ
				|	РегистрНакопления.Занятия.Обороты(
				|			&НачПериода,
				|			&КонПериода,
				|			Запись,
				|			Предмет = &Предмет
				|				И Студент В
				|					(ВЫБРАТЬ
				|						ВсеСтуденты.Ссылка КАК Ссылка
				|					ИЗ
				|						ВсеСтуденты КАК ВсеСтуденты)
				|				И Преподаватель = &Преподаватель
				|				И ПериодУчебы = &ПериодУчебы) КАК ЗанятияОбороты
				|
				|УПОРЯДОЧИТЬ ПО
				|	РегистраторДатаЗанятия
				|ИТОГИ ПО
				|	РегистраторДата";
			
			Запрос.УстановитьПараметр("МасСтуд", СтудентыДляОтбора);
			Запрос.УстановитьПараметр("КонПериода", КонецДня(Дата));
			Запрос.УстановитьПараметр("НачПериода", НачалоДня(Дата));
			Запрос.УстановитьПараметр("Предмет", Предмет);
	        Запрос.УстановитьПараметр("Преподаватель", ПараметрыСеанса.ТекПользователь);
			Запрос.УстановитьПараметр("ПериодУчебы", УчебныйГод.Период);
	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДата = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаДата.Следующий() Цикл
		// Вставить обработку выборки ВыборкаДата
		
		КоличествоПропусков = 0;
		МассивСтудентовПропускающих = Новый Массив;
		
		ВыборкаДетальныеЗаписи = ВыборкаДата.Выбрать();
		
		
		
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			
			 Если ВыборкаДетальныеЗаписи.ТипБалла = Перечисления.ТипыБаллов.Отсутствие Тогда
			 
			 	КоличествоПропусков = КоличествоПропусков + 1;
			    МассивСтудентовПропускающих.Добавить(ВыборкаДетальныеЗаписи.Студент);
				ДатаДляСтруктуры = ВыборкаДетальныеЗаписи.РегистраторДата;
				НомерДляСтруктуры = ВыборкаДетальныеЗаписи.НомерПары;
			 КонецЕсли;
		КонецЦикла;
		
		Показатели = Новый Структура;
	    Показатели.Вставить("Дата",ДатаДляСтруктуры);
		Показатели.Вставить("НомерПары",НомерДляСтруктуры);
		Показатели.Вставить("КоличествоПропусков",КоличествоПропусков);
		Показатели.Вставить("МассивСтудентовПропускающих",МассивСтудентовПропускающих);

		МассивЗанятий.Добавить(Показатели);
		
	КонецЦикла;
	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	
    Возврат МассивЗанятий;
	
КонецФункции // ПолучитьСтатистикуПосещенияЗаОдинДень()

 // Получает статистику посещения и успеваемости студентами одной группы для отчета о успеваемости и посещаемости
//
// Параметры:
//  Предмет  - СправочникСсылка.Предметы - содержит ссылку на предмет
//  Группа  - СправочникСсылка.Группы - содержит ссылку на группу
//  Дата1  - Дата - содержит начальную дату
//  Дата2  - Дата - содержит конечную дату
//  ВыполнятьОтборПодгруппы  - Булево - содержит сведения об отборе студентов своей подгруппы
//
// Возвращаемое значение:
//   Массив   - Содержит структуры (КоличествоПропусков,КоличествоОценок,КоличествоПятерок,КоличествоЧетверок,КоличествоТроек,КоличествоДвоек - Число)                                     
//
Функция ПолучитьСтатистикуУспеваемостиНаОснованииАттестации(Предмет, Группа, Дата1, Дата2, ВыполнятьОтборПодгруппы) Экспорт

	УчебныйГод = РаботаССеансом.ПолучитьНачалоИКонецУчебногоГода();
	Показатели = Новый Структура;
	Если Не ВыполнятьОтборПодгруппы Тогда
		
		СтудентыДляОтбора = ПолучитьВыборкуСтудентовГруппы(Группа, Дата2)[0];
	Иначе
		
		СтудентыДляОтбора = ВыбратьСтудентовСвоейПодгруппы(Группа, Предмет, Дата2);
		
	КонецЕсли;	

			  //{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
				// Данный фрагмент построен конструктором.
				// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
				
				Запрос = Новый Запрос;
				Запрос.Текст = 
					"ВЫБРАТЬ
					|	Студенты.Ссылка КАК Ссылка,
					|	Студенты.Наименование КАК Наименование
					|ПОМЕСТИТЬ ВсеСтуденты
					|ИЗ
					|	Справочник.Студенты КАК Студенты
					|ГДЕ
					|	Студенты.Отчислен = ЛОЖЬ
					|	И Студенты.Ссылка В(&МасСтуд)
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|ВЫБРАТЬ
					|	АттестацияОбороты.Период КАК Период,
					|	АттестацияОбороты.Предмет КАК Предмет,
					|	АттестацияОбороты.Студент КАК Студент,
					|	АттестацияОбороты.БаллОборот КАК БаллОборот
					|ИЗ
					|	РегистрНакопления.Аттестация.Обороты(
					|			&НачПериода,
					|			&КонПериода,
					|			Запись,
					|			Предмет = &Предмет
					|				И Студент В
					|					(ВЫБРАТЬ
					|						ВсеСтуденты.Ссылка КАК Ссылка
					|					ИЗ
					|						ВсеСтуденты КАК ВсеСтуденты)
					|				И Преподаватель = &Преподаватель
					|				И ПериодУчебы = &ПериодУчебы
					|				И ТипАттестации = &А) КАК АттестацияОбороты";
				
				Запрос.УстановитьПараметр("МасСтуд", СтудентыДляОтбора);
				Запрос.УстановитьПараметр("КонПериода", КонецДня(Дата2));
				Запрос.УстановитьПараметр("НачПериода", НачалоДня(Дата1));
				Запрос.УстановитьПараметр("Предмет", Предмет);
	            Запрос.УстановитьПараметр("Преподаватель", ПараметрыСеанса.ТекПользователь);
				Запрос.УстановитьПараметр("ПериодУчебы", УчебныйГод.Период);
				Запрос.УстановитьПараметр("А", Перечисления.ТипАттестации.Аттестация);
	 
	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
		КоличествоДвоек = 0;
		КоличествоТроек = 0;
		КоличествоЧетверок = 0;
		КоличествоПятерок = 0;
		КоличествоОценок = 0;
		
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		// Вставить обработку выборки ВыборкаДата
			 
				Если ВыборкаДетальныеЗаписи.БаллОборот = 2 Тогда
				
					КоличествоДвоек = КоличествоДвоек + 1;
				
				Иначе Если ВыборкаДетальныеЗаписи.БаллОборот = 3 Тогда
				
					КоличествоТроек = КоличествоТроек + 1;
					
				Иначе Если ВыборкаДетальныеЗаписи.БаллОборот = 4 Тогда
				
					КоличествоЧетверок = КоличествоЧетверок + 1;
					
				Иначе Если ВыборкаДетальныеЗаписи.БаллОборот = 5 Тогда
				
					КоличествоПятерок = КоличествоПятерок + 1;
					
				КонецЕсли;	
				КонецЕсли;	
				КонецЕсли;
				КонецЕсли; 
				
				КоличествоОценок = КоличествоОценок + 1;

		КонецЦикла;
		
		
		Показатели.Вставить("КоличествоОценок",КоличествоОценок);
		Показатели.Вставить("КоличествоПятерок",КоличествоПятерок);
		Показатели.Вставить("КоличествоЧетверок",КоличествоЧетверок);
		Показатели.Вставить("КоличествоТроек",КоличествоТроек);
		Показатели.Вставить("КоличествоДвоек",КоличествоДвоек);

	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	
    Возврат Показатели;
	
КонецФункции // ПолучитьСтатистикуУспеваемостиНаОснованииАттестации()

 // Возвращает список студентов только своей подгруппы
//
// Параметры:
//  Группа  - СправочникСсылка.Группы - содержит ссылку на группу
//  Предмет  - СправочникСсылка.Предметы - содержит ссылку на предмет
//
// Возвращаемое значение:
//   Массив   - Содержит ссылки на студентов СправочникСсылка.Студенты                                     
// 
 Функция ВыбратьСтудентовСвоейПодгруппы(Группа, Предмет, Дата) Экспорт
	 
	ОтобранныеСтуденты = Новый Массив;
	
	ПредварительноОтобранныеСтуденты = Новый Массив;
	ПредварительноОтобранныеСсылки = Новый Массив;
	ПредварительноОтобранныеОтчисленные = Новый Массив;
	
	ПолнаяВыборкаСтудентов = ПолучитьВыборкуСтудентовГруппы(Группа, Дата);
	Ссылки = РаботаСКонтингентом.ПолучитьСсылкиНаРесурсы(Предмет, Группа);
	Если Ссылки.Количество() = 0 Тогда
	
		 Возврат ПолнаяВыборкаСтудентов[0];
	
	КонецЕсли;
	НачСтуд = Ссылки[2];
	КонСтуд = Ссылки[3];
	
	//если группа не разделена
	Если Ссылки[2] = 0 ИЛИ Ссылки[3] = 0 Тогда
		
		Для н = 1 По ПолнаяВыборкаСтудентов[0].Количество() Цикл
		
			 Если ПолнаяВыборкаСтудентов[1][н-1] = 0 Тогда
			 
			 	   ОтобранныеСтуденты.Добавить(ПолнаяВыборкаСтудентов[0][н-1]);
			 
			 КонецЕсли;
		
		 КонецЦикла;
		Возврат ОтобранныеСтуденты;
		
	КонецЕсли;
	
	//если группа разделена		   
	Для н = 1 По ПолнаяВыборкаСтудентов[0].Количество() Цикл
				 
				 Если НачСтуд > н ИЛИ КонСтуд < н Тогда
				 
				 	  ПредварительноОтобранныеСсылки.Добавить(ПолнаяВыборкаСтудентов[0][н-1]);
					  ПредварительноОтобранныеОтчисленные.Добавить(ПолнаяВыборкаСтудентов[1][н-1]);
				 
				 КонецЕсли;
		
			 КонецЦикла;
			 ПредварительноОтобранныеСтуденты.Добавить(ПредварительноОтобранныеСсылки);
			 ПредварительноОтобранныеСтуденты.Добавить(ПредварительноОтобранныеОтчисленные);
			 
	Для н = 1 По ПредварительноОтобранныеСтуденты[0].Количество() Цикл
		
			 Если ПредварительноОтобранныеСтуденты[1][н-1] = 0 Тогда
			 
			 	   ОтобранныеСтуденты.Добавить(ПредварительноОтобранныеСтуденты[0][н-1]);
			 
			 КонецЕсли;
		
		 КонецЦикла;
	Возврат ОтобранныеСтуденты; 
 
 КонецФункции // ВыбратьСтудентовСвоейПодгруппы()

  // Возвращает список студентов только своей подгруппы
//
// Параметры:
//  Группа  - СправочникСсылка.Группы - содержит ссылку на группу
//  Предмет  - СправочникСсылка.Предметы - содержит ссылку на предмет
//
// Возвращаемое значение:
//   Число   - Количество студентов отобранной подгруппы                                     
//
 Функция ПолучитьКоличествоСтудентовОтобраннойГруппы(Группа, Предмет, Дата) Экспорт
	 
	 КолвоРеалСтуд = 0;
	  МасСтуд = ВыбратьСтудентовСвоейПодгруппы(Группа, Предмет, Дата);
	  Для каждого студ Из МасСтуд Цикл
	  
	  	   Если Не студ.Отчислен Тогда
		   
		   	   КолвоРеалСтуд = КолвоРеалСтуд + 1;
		   
		   КонецЕсли;
	  
	  КонецЦикла;
	 
 	  Возврат КолвоРеалСтуд;
 
  КонецФункции // ПолучитьКоличествоСтудентовОтобраннойГруппы()

// Проверяет, проводилась ли аттестация на основании данного занятия
//
// Параметры:
//  Ссылка  - ДокументСсылка.ПроведениеЗанятия - содержит ссылку на занятие
//
// Возвращаемое значение:
//   Булево   - Была или не была проведена аттестация                                     
//
Функция ИмеетсяАттестацияПоДокументуОснованию(Ссылка) Экспорт
	
	СсылкаНаАттестацию = Документы.ПроведениеАттестации.НайтиПоРеквизиту("ДокументОснование", Ссылка).Пустая();
	Если НЕ СсылкаНаАттестацию Тогда
	
		Возврат Истина;
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;

КонецФункции // ИмеетсяАттестацияПоДокументуОснованию()

// Проверяет, затарифицирован ли данный предмет с данной группой за преподавателем (на основании нагрузки)
//
// Параметры:
//  Группа  - СправочникСсылка.Группы - содержит ссылку на группу
//  Предмет  - СправочникСсылка.Предметы - содержит ссылку на предмет
//  Дата  - Дата - текущая дата для определения номера семестра
//
// Возвращаемое значение:
//   Булево   - Была или не была проведена тарификация                                     
//
Функция ИмеетсяТарификацияДляГруппыПредмета(Группа, Предмет, Дата) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	1 КАК Семестр,
		|	ФормированиеНагрузкиНагрузка1.Предмет КАК Предмет,
		|	ФормированиеНагрузкиНагрузка1.Группа КАК Группа
		|ИЗ
		|	Документ.ФормированиеНагрузки.Нагрузка1 КАК ФормированиеНагрузкиНагрузка1
		|ГДЕ
		|	ФормированиеНагрузкиНагрузка1.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	2,
		|	ФормированиеНагрузкиНагрузка2.Предмет,
		|	ФормированиеНагрузкиНагрузка2.Группа
		|ИЗ
		|	Документ.ФормированиеНагрузки.Нагрузка2 КАК ФормированиеНагрузкиНагрузка2
		|ГДЕ
		|	ФормированиеНагрузкиНагрузка2.Ссылка = &Ссылка";
	
	Ссылка = РегистрыСведений.НагрузкиПреподавателей.ПолучитьПоследнее(, Новый Структура("Преподаватель", ПараметрыСеанса.ТекПользователь)).Нагрузка;
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если Месяц(Дата) >= 1 И Месяц(Дата) <= 7 Тогда
		
			Если ВыборкаДетальныеЗаписи.Семестр = 2 И ВыборкаДетальныеЗаписи.Группа = Группа И ВыборкаДетальныеЗаписи.Предмет = Предмет Тогда
			
				  Возврат Истина;
			
			КонецЕсли;
		
		Иначе
		
			Если ВыборкаДетальныеЗаписи.Семестр = 1 И ВыборкаДетальныеЗаписи.Группа = Группа И ВыборкаДетальныеЗаписи.Предмет = Предмет Тогда
			
				  Возврат Истина;
			
			КонецЕсли;
		
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;

КонецФункции // ИмеетсяТарификацияДляГруппыПредмета()

 // Возвращает блок информации для заполнения занятия начальными данными из расписания
//
// Параметры:
//  НомерПары  - Число - содержит номер пары внутри даты занятия
//  ДатаЗанятия  - Дата - содержит дату занятия
//
// Возвращаемое значение:
//   Массив[6]   - Содержит Тему, ДЗ, Колво часов, совмещение ли, замена ли, аттестация ли                                     
//
Функция ПолучитьНазваниеТемыЧасовСовмещенияИДЗ(НомерПары,ДатаЗанятия) Экспорт
	
	УчебныйГод = РаботаССеансом.ПолучитьНачалоИКонецУчебногоГода();
	
	Мас = Новый Массив;
		//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасписаниеСрезПоследних.Тема КАК Тема,
		|	РасписаниеСрезПоследних.Дз КАК Дз,
		|	РасписаниеСрезПоследних.НомерПары КАК НомерПары,
		|	РасписаниеСрезПоследних.Период КАК Период,
		|	РасписаниеСрезПоследних.КолвоЧасов КАК КолвоЧасов,
		|	РасписаниеСрезПоследних.Замена КАК Замена,
		|	РасписаниеСрезПоследних.Совмещение КАК Совмещение,
		|	РасписаниеСрезПоследних.Аттестация КАК Аттестация
		|ИЗ
		|	РегистрСведений.Расписание.СрезПоследних(
		|			&ТекДата,
		|			НомерПары = &НомерПары
		|				И Период = &Период
		|				И Преподаватель = &Преподаватель
		|				И ПериодУчебы = &ПериодУчебы) КАК РасписаниеСрезПоследних";
	
	Запрос.УстановитьПараметр("НомерПары", НомерПары);
	Запрос.УстановитьПараметр("Период", ДатаЗанятия);
	Запрос.УстановитьПараметр("ТекДата", ДатаЗанятия);
	Запрос.УстановитьПараметр("Преподаватель", ПараметрыСеанса.ТекПользователь);
	Запрос.УстановитьПараметр("ПериодУчебы", УчебныйГод.Период);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ТекТема = ВыборкаДетальныеЗаписи.Тема; 
		ТекДз =  ВыборкаДетальныеЗаписи.Дз;
		ТекКолвоЧасов =  ВыборкаДетальныеЗаписи.КолвоЧасов;
		ЭтоСовмещение =  ВыборкаДетальныеЗаписи.Совмещение;
		ЭтоЗамена =  ВыборкаДетальныеЗаписи.Замена;
		ЭтоАттестация =  ВыборкаДетальныеЗаписи.Аттестация;
	КонецЦикла;
	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	Мас.Добавить(ТекТема);
	Мас.Добавить(ТекДз);
	Мас.Добавить(ТекКолвоЧасов);
	Мас.Добавить(ЭтоСовмещение);
	Мас.Добавить(ЭтоЗамена);
	Мас.Добавить(ЭтоАттестация);
    Возврат Мас;
	
КонецФункции // ПолучитьНазваниеТемыЧасовСовмещенияИДЗ()

 // Возвращает список тем которые пропустил студент за месяц
//
// Параметры:
//  Группа  - СправочникСсылка.Группы - содержит ссылку на группу
//  Предмет  - СправочникСсылка.Предметы - содержит ссылку на предмет
//  Месяц  - Число - месяц формирования
//  Дата  - Дата - содержит дату формирования (для года)
//  Студент  - СправочникСсылка.Студенты - содержит ссылку на студента
//
// Возвращаемое значение:
//   Массив - Содержит пропущенные темы Структуру (Дата и Тема)                                    
//
Функция ПолучитьСписокПропущенныхТемСтудентомЗаМесяц(Группа,Предмет, Месяц, Дата, Студент) Экспорт
	
	ПропущенныеТемы = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПроведениеЗанятия.Тема КАК Тема,
		|	МЕСЯЦ(ПроведениеЗанятия.ДатаЗанятия) КАК Месяц,
		|	ГОД(ПроведениеЗанятия.ДатаЗанятия) КАК Год,
		|	ПроведениеЗанятия.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ Сводная
		|ИЗ
		|	Документ.ПроведениеЗанятия КАК ПроведениеЗанятия
		|ГДЕ
		|	ПроведениеЗанятия.Группа = &Группа
		|	И ПроведениеЗанятия.Предмет = &Предмет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Сводная.Тема КАК Тема,
		|	ЗанятияОбороты.ТипБалла КАК ТипБалла,
		|	ЗанятияОбороты.БаллОборот КАК БаллОборот,
		|	ЗанятияОбороты.Период КАК Период,
		|	""Пропуск"" КАК ТипПоля
		|ИЗ
		|	Сводная КАК Сводная
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.Занятия.Обороты(
		|				,
		|				,
		|				Запись,
		|				ТипБалла = &ТипБалла
		|					И Студент = &Студент
		|					И Предмет = &Предмет) КАК ЗанятияОбороты
		|		ПО Сводная.Ссылка = ЗанятияОбороты.Регистратор
		|ГДЕ
		|	Сводная.Месяц = &Месяц
		|	И Сводная.Год = &Год
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Сводная.Тема,
		|	ЗанятияОбороты.ТипБалла,
		|	ЗанятияОбороты.БаллОборот,
		|	ЗанятияОбороты.Период,
		|	""Неудовлетворительно""
		|ИЗ
		|	Сводная КАК Сводная
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.Занятия.Обороты(
		|				,
		|				,
		|				Запись,
		|				ТипБалла = &ТипБаллаО
		|					И Студент = &Студент
		|					И Предмет = &Предмет) КАК ЗанятияОбороты
		|		ПО Сводная.Ссылка = ЗанятияОбороты.Регистратор
		|ГДЕ
		|	Сводная.Месяц = &Месяц
		|	И Сводная.Год = &Год
		|	И ЗанятияОбороты.БаллОборот = 2
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период";
	
	Запрос.УстановитьПараметр("Группа", Группа);
	Запрос.УстановитьПараметр("Предмет", Предмет);
	Запрос.УстановитьПараметр("Месяц", Месяц);
	Запрос.УстановитьПараметр("Год", Год(Дата));
	Запрос.УстановитьПараметр("ТипБалла", Перечисления.ТипыБаллов.Отсутствие);
	Запрос.УстановитьПараметр("ТипБаллаО", Перечисления.ТипыБаллов.Оценка);
	Запрос.УстановитьПараметр("Студент", Студент);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СтруктураПропуска = Новый Структура;
		СтруктураПропуска.Вставить("Дата",Лев(ВыборкаДетальныеЗаписи.Период,5));
		СтруктураПропуска.Вставить("Тема",ВыборкаДетальныеЗаписи.Тема);
		
		ПропущенныеТемы.Добавить(СтруктураПропуска);
		
	КонецЦикла;
	
	Возврат ПропущенныеТемы;
	
КонецФункции // ПолучитьСписокПропущенныхТемСтудентомЗаМесяц()
 





 // Получает массив проведенных аттестация по предмету и группе
//
// Параметры:
//  Группа  - СправочникСсылка.Группы - содержит ссылку на группу
//  Предмет  - СправочникСсылка.Предметы - содержит ссылку на предмет
//  Интервал  - Дата - период формирования учебного года
//
// Возвращаемое значение:
//   Массив  - Структуры (Студент - СправочникСсылка.Студенты, Период - Дата, Оценка - Число, 
//				ИсправленнаяОценка - Число, Ссылка - НавигационнаяСсылка на аттестацию)
//
Функция ПолучитьАттестацииНаСервере(Группа, Предмет, Интервал) Экспорт
	
	УчебныйГод = РаботаССеансом.ПолучитьНачалоИКонецУчебногоГода();
	Если Интервал <> Дата("00010101") Тогда
		
		УчебныйГод = РаботаССеансом.ПолучитьНачалоИКонецУчебногоГодаЛюбой(Интервал);
	
	КонецЕсли;
	МассивАттестаций = Новый Массив;
	
	//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АттестацияОбороты.Период КАК Период,
		|	АттестацияОбороты.Предмет КАК Предмет,
		|	АттестацияОбороты.Студент КАК Студент,
		|	АттестацияОбороты.ТипАттестации КАК ТипАттестации,
		|	АттестацияОбороты.БаллОборот КАК БаллОборот,
		|	АттестацияОбороты.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрНакопления.Аттестация.Обороты(
		|			&Нач,
		|			&Кон,
		|			Запись,
		|			Предмет = &Предмет
		|				И Студент В
		|					(ВЫБРАТЬ
		|						Студенты.Ссылка КАК Ссылка
		|					ИЗ
		|						Справочник.Студенты КАК Студенты
		|					ГДЕ
		|						Студенты.Группа = &Группа)
		|				И Преподаватель = &Преподаватель
		|				И ПериодУчебы = &ПериодУчебы) КАК АттестацияОбороты
		|
		|УПОРЯДОЧИТЬ ПО
		|	Студент,
		|	ТипАттестации
		|ИТОГИ ПО
		|	Студент,
		|	Период";
	
	Запрос.УстановитьПараметр("Кон", УчебныйГод.КонецГода);
	Запрос.УстановитьПараметр("Нач", УчебныйГод.НачалоГода);
	Запрос.УстановитьПараметр("Преподаватель", ПараметрыСеанса.ТекПользователь);
	Запрос.УстановитьПараметр("ПериодУчебы", УчебныйГод.Период);
	Запрос.УстановитьПараметр("Группа", Группа);
	Запрос.УстановитьПараметр("Предмет", Предмет);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаСтудент = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаСтудент.Следующий() Цикл
		// Вставить обработку выборки ВыборкаСтудент
	
		ВыборкаПериоды = ВыборкаСтудент.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		
		
		Пока ВыборкаПериоды.Следующий() Цикл
			
			ИтоговаяОценка = 0;
			ИсправленнаяОценка = 0;
			СтруктураАттестации = Новый Структура;
			
			ВыборкаДетальныеЗаписи = ВыборкаПериоды.Выбрать();
			
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Если ВыборкаДетальныеЗаписи.ТипАттестации = Перечисления.ТипАттестации.Аттестация Тогда
			
				ИтоговаяОценка = ВыборкаДетальныеЗаписи.БаллОборот;

			КонецЕсли;
			Если ВыборкаДетальныеЗаписи.ТипАттестации = Перечисления.ТипАттестации.Исправление И ВыборкаДетальныеЗаписи.БаллОборот > 0 Тогда
			
				ИсправленнаяОценка = ВыборкаДетальныеЗаписи.БаллОборот; 

			КонецЕсли;
			
			СтруктураАттестации.Вставить("Студент", ВыборкаДетальныеЗаписи.Студент);
			СтруктураАттестации.Вставить("Период", ВыборкаДетальныеЗаписи.Период);
			СтруктураАттестации.Вставить("Оценка", ИтоговаяОценка);
			СтруктураАттестации.Вставить("ИсправленнаяОценка", ИсправленнаяОценка);
			СтруктураАттестации.Вставить("Ссылка", ПолучитьНавигационнуюСсылку(ВыборкаДетальныеЗаписи.Регистратор));
		    
		КонецЦикла;
			МассивАттестаций.Добавить(СтруктураАттестации);
        КонецЦикла;
		
	КонецЦикла;
	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА

	Возврат МассивАттестаций;

КонецФункции // ПолучитьАттестацииНаСервере()

 // Получает аттестации для одного дня, когда эта аттестация не последняя в журнале
//
// Параметры:
//  Аттестация  - Массив - Массив всех аттестаций из ПолучитьАттестацииНаСервере()
//  Дата  - Дата - дата, на которую получить аттестацию
//
// Возвращаемое значение:
//   Массив  - Структуры (Студент - СправочникСсылка.Студенты, Оценка - Число, 
//				ИсправленнаяОценка - Число, Ссылка - Ссылка на аттестацию)
//
Функция ПолучитьМассивАттестацииОдногоДняЕслиДатаБольшеАттестации(Аттестация, Дата) Экспорт
	
	МассивАттестацийОдногоДня = Новый Массив;
	
	Для каждого Стр Из Аттестация Цикл
	
		 Если Стр.Период < Дата И Стр.Период > НачалоМесяца(НачалоМесяца(Дата)-1) Тогда
			 
			 СтруктураДня = Новый Структура;
		 	 СтруктураДня.Вставить("Студент", Стр.Студент);
			 СтруктураДня.Вставить("Оценка", Стр.Оценка);
			 СтруктураДня.Вставить("ИсправленнаяОценка", Стр.ИсправленнаяОценка);
			 СтруктураДня.Вставить("Ссылка", Стр.Ссылка);
		     МассивАттестацийОдногоДня.Добавить(СтруктураДня);
			 
		 КонецЕсли;
	
	КонецЦикла;	
	
	Возврат МассивАттестацийОдногоДня;
	
КонецФункции // ПолучитьМассивАттестацииОдногоДняЕслиДатаБольшеАттестации()

 // Получает аттестации для одного дня, когда эта аттестация последняя в журнале
//
// Параметры:
//  Аттестация  - Массив - Массив всех аттестаций из ПолучитьАттестацииНаСервере()
//  Дата  - Дата - дата, на которую получить аттестацию
//
// Возвращаемое значение:
//   Массив  - Структуры (Студент - СправочникСсылка.Студенты, Оценка - Число, 
//				ИсправленнаяОценка - Число, Ссылка - Ссылка на аттестацию)
//
Функция ПолучитьМассивАттестацииОдногоДняЕслиДатаМеньшеАттестации(Аттестация, Дата) Экспорт
	
	МассивАттестацийОдногоДня = Новый Массив;
	
	Для каждого Стр Из Аттестация Цикл
	
		 Если Стр.Период > Дата Тогда
			 
			 СтруктураДня = Новый Структура;
		 	 СтруктураДня.Вставить("Студент", Стр.Студент);
			 СтруктураДня.Вставить("Оценка", Стр.Оценка);
			 СтруктураДня.Вставить("ИсправленнаяОценка", Стр.ИсправленнаяОценка);
			 СтруктураДня.Вставить("Ссылка", Стр.Ссылка);
		     МассивАттестацийОдногоДня.Добавить(СтруктураДня);
			 
		 КонецЕсли;
	
	КонецЦикла;	
	
	Возврат МассивАттестацийОдногоДня;
	
КонецФункции // ПолучитьМассивАттестацииОдногоДняЕслиДатаБольшеАттестации()

 // Получает оценку в аттестации из ПолучитьМассивАттестацииОдногоДня()
//
// Параметры:
//  МассивАттестацийОдногоДня  - Массив - Массив аттестаций дня из ПолучитьМассивАттестацииОдногоДня()
//  Студент  - СправочникСсылка.Студенты
//
// Возвращаемое значение:
//   Структура (А - Число - оценка аттестации, И - Число - оценка исправления аттестации)
//
Функция ПолучитьОценкуВАттестацииОдногоДня(МассивАттестацийОдногоДня, Студент) Экспорт
	
	ОценкаСтруктура = Новый Структура;
	
	  Для каждого Стр Из МассивАттестацийОдногоДня Цикл
	  
	  	   Если Стр.Студент = Студент Тогда
			   
			   ОценкаСтруктура.Вставить("А", Стр.Оценка);
			   ОценкаСтруктура.Вставить("И", Стр.ИсправленнаяОценка);
		   	   Возврат ОценкаСтруктура;
		   
		   КонецЕсли;
	  
	  КонецЦикла;
	  
	  ОценкаСтруктура.Вставить("А", "");
	  ОценкаСтруктура.Вставить("И", "");
	  Возврат ОценкаСтруктура;
	  
  КонецФункции // ПолучитьОценкуВАттестацииОдногоДня()

// Получает оценку текущих занятий из всех оценок студента по предмету
//
// Параметры:
//  Мас  - Массив - Все оценки студента
//  ДатаЖурнала  - Дата - Дата, на которую получается оценка
//  НомерПары  - Число - Номер пары в дне
//
// Возвращаемое значение:
//   Строка - оценка либо нб
//
Функция ПолучитьОценкуИзМассиваОценок(Мас, ДатаЖурнала, НомерПары) Экспорт
	   
		Для каждого СтруктураСтудента Из Мас Цикл
            Если СтруктураСтудента.Балл > 0 Тогда
			  		
				 Если Лев(СтруктураСтудента.ДеньЗанятия,5) = ДатаЖурнала И СтруктураСтудента.НомерПары = НомерПары Тогда
						Если СтруктураСтудента.ТипБалла = Перечисления.ТипыБаллов.Оценка Тогда
						
							Возврат СтруктураСтудента.Балл; 
						
						Иначе Если СтруктураСтудента.ТипБалла = Перечисления.ТипыБаллов.Отсутствие Тогда
						
							Возврат "нб";
						
						КонецЕсли;
						КонецЕсли;
		
				КонецЕсли;
		    КонецЕсли;
		КонецЦикла;
	
	Возврат "";

КонецФункции // ПолучитьОценкуИзМассиваОценок()

// Получает даты для журнала
//
// Параметры:
//  Предмет  - СправочникСсылка.Предметы - содержит ссылку на предмет
//  Группа  - СправочникСсылка.Группы - содержит ссылку на группу
//  Интервал  - Дата - период формирования учебного год
//
// Возвращаемое значение:
//   Массив - Структуры (Дата - Дата, НомерПары - Число, СсылкаНаУрок - НавигационнаяСсылка - Ссылка на занятие) 
//
Функция СформироватьДатыЖурналаНаСервере(Предмет, Группа, Интервал) Экспорт
	
	УчебныйГод = РаботаССеансом.ПолучитьНачалоИКонецУчебногоГода();
	Если Интервал <> Дата("00010101") Тогда
		
		УчебныйГод = РаботаССеансом.ПолучитьНачалоИКонецУчебногоГодаЛюбой(Интервал);
	
	КонецЕсли;
	
	
	МассивДат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Расписание.Период КАК Период,
		|	Расписание.НомерПары КАК НомерПары,
		|	Расписание.СсылкаНаУрок КАК СсылкаНаУрок
		|ИЗ
		|	РегистрСведений.Расписание КАК Расписание
		|ГДЕ
		|	Расписание.Предмет = &Предмет
		|	И Расписание.Группа = &Группа
		|	И Расписание.Преподаватель = &Преподаватель
		|	И Расписание.ПериодУчебы = &ПериодУчебы
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период,
		|	НомерПары";
	
	Запрос.УстановитьПараметр("Группа", Группа);
	Запрос.УстановитьПараметр("Предмет", Предмет);
	Запрос.УстановитьПараметр("Преподаватель", ПараметрыСеанса.ТекПользователь);
	Запрос.УстановитьПараметр("ПериодУчебы", УчебныйГод.Период);

	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		СтруктураДаты = Новый Структура;
		СтруктураДаты.Вставить("Дата", ВыборкаДетальныеЗаписи.Период);
		СтруктураДаты.Вставить("НомерПары", ВыборкаДетальныеЗаписи.НомерПары);
		СтруктураДаты.Вставить("СсылкаНаУрок", ПолучитьНавигационнуюСсылку(ВыборкаДетальныеЗаписи.СсылкаНаУрок));
		МассивДат.Добавить(СтруктураДаты);
		
	КонецЦикла;
	

    Возврат МассивДат;
	
КонецФункции 

// Получает текущие оценки студентов для журнала
//
// Параметры:
//  Предмет  - СправочникСсылка.Предметы - содержит ссылку на предмет
//  Группа  - СправочникСсылка.Группы - содержит ссылку на группу
//  Интервал  - Дата - период формирования учебного год
//
// Возвращаемое значение:
//   Массив - Массив, содержащий Структуры, отсортированные по студентам (Студент - Ссылка, ДеньЗанятия - Дата, ТипБалла - Перечисление, Балл - Число, 
//				Отчислен - Булево, НомерПары - Число) 
//
Функция ПолучитьОценкиСтудентовНаСервере(Предмет, Группа, Интервал) Экспорт
	
	УчебныйГод = РаботаССеансом.ПолучитьНачалоИКонецУчебногоГода();
	Если Интервал <> Дата("00010101") Тогда
		
		УчебныйГод = РаботаССеансом.ПолучитьНачалоИКонецУчебногоГодаЛюбой(Интервал);
	
	КонецЕсли;
	МассивОценок = Новый Массив;
	
	//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Студенты.Наименование КАК Наименование,
		|	Студенты.Ссылка КАК Ссылка,
		|	Студенты.Отчислен КАК Отчислен
		|ПОМЕСТИТЬ ВсеСтуденты
		|ИЗ
		|	Справочник.Студенты КАК Студенты
		|ГДЕ
		|	Студенты.Группа = &Группа
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗанятияОбороты.Студент КАК Студент,
		|	ЗанятияОбороты.Период КАК ДеньЗанятия,
		|	ЗанятияОбороты.ТипБалла КАК ТипБалла,
		|	ЗанятияОбороты.БаллОборот КАК БаллОборот,
		|	ЗанятияОбороты.НомерПары КАК НомерПары
		|ПОМЕСТИТЬ ВсеОценки
		|ИЗ
		|	РегистрНакопления.Занятия.Обороты(
		|			&НачГода,
		|			&КонГода,
		|			Регистратор,
		|			Предмет = &Предмет
		|				И Студент В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						ВсеСтуденты.Ссылка КАК Ссылка
		|					ИЗ
		|						ВсеСтуденты КАК ВсеСтуденты)
		|				И Преподаватель = &Преподаватель
		|				И ПериодУчебы = &ПериодУчебы) КАК ЗанятияОбороты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВсеОценки.ДеньЗанятия КАК ДеньЗанятия,
		|	ВсеОценки.ТипБалла КАК ТипБалла,
		|	ЕСТЬNULL(ВсеОценки.БаллОборот, 0) КАК Балл,
		|	ВсеСтуденты.Ссылка КАК Ссылка,
		|	ВсеСтуденты.Отчислен КАК Отчислен,
		|	ВсеОценки.НомерПары КАК НомерПары,
		|	ВсеСтуденты.Наименование КАК Наименование
		|ИЗ
		|	ВсеСтуденты КАК ВсеСтуденты
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВсеОценки КАК ВсеОценки
		|		ПО ВсеСтуденты.Ссылка = ВсеОценки.Студент
		|
		|УПОРЯДОЧИТЬ ПО
		|	Наименование,
		|	ДеньЗанятия,
		|	НомерПары
		|ИТОГИ
		|	КОЛИЧЕСТВО(Балл)
		|ПО
		|	Ссылка";
	
	Запрос.УстановитьПараметр("Группа", Группа);
	Запрос.УстановитьПараметр("КонГода", УчебныйГод.КонецГода);
	Запрос.УстановитьПараметр("НачГода", УчебныйГод.НачалоГода);
	Запрос.УстановитьПараметр("Предмет", Предмет);
	Запрос.УстановитьПараметр("Преподаватель", ПараметрыСеанса.ТекПользователь);
	Запрос.УстановитьПараметр("ПериодУчебы", УчебныйГод.Период);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаСсылка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаСсылка.Следующий() Цикл
		// Вставить обработку выборки ВыборкаСсылка
		
		МассивОценокСтрукт = Новый Массив;
		 
		ВыборкаДетальныеЗаписи = ВыборкаСсылка.Выбрать();
	
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
				СтруктураОценки = Новый Структура;
				СтруктураОценки.Вставить("Студент", ВыборкаДетальныеЗаписи.Ссылка);
				СтруктураОценки.Вставить("ДеньЗанятия", ВыборкаДетальныеЗаписи.ДеньЗанятия);
				СтруктураОценки.Вставить("ТипБалла", ВыборкаДетальныеЗаписи.ТипБалла);
				СтруктураОценки.Вставить("Балл", ВыборкаДетальныеЗаписи.Балл);
				СтруктураОценки.Вставить("Отчислен", ВыборкаДетальныеЗаписи.Отчислен);
				СтруктураОценки.Вставить("НомерПары", ВыборкаДетальныеЗаписи.НомерПары);
			    МассивОценокСтрукт.Добавить(СтруктураОценки);
			
		КонецЦикла;
		
		МассивОценок.Добавить(МассивОценокСтрукт);
		
	КонецЦикла;
	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА

	
	Возврат МассивОценок;

КонецФункции // ПолучитьОценкиСтудентовНаСервере()

// Получает список тем для журнала
//
// Параметры:
//  Предмет  - СправочникСсылка.Предметы - содержит ссылку на предмет
//  Группа  - СправочникСсылка.Группы - содержит ссылку на группу
//  Интервал  - Дата - период формирования учебного год
//
// Возвращаемое значение:
//   Массив - Структуры (Период - Дата, Тема,Дз - Строка, Замена - Булево, КолвоЧасов - Число, 
//				ЗаменяющийПреподаватель - Ссылка) 
//   
Функция ПолучитьСписокТемДляЖурналаНаСервере(Предмет, Группа, Интервал) Экспорт

		//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	УчебныйГод = РаботаССеансом.ПолучитьНачалоИКонецУчебногоГода();
	Если Интервал <> Дата("00010101") Тогда
		
		УчебныйГод = РаботаССеансом.ПолучитьНачалоИКонецУчебногоГодаЛюбой(Интервал);
	
	КонецЕсли;
	МассивСтруктур = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Расписание.Период КАК Период,
		|	Расписание.Тема КАК Тема,
		|	Расписание.Дз КАК Дз,
		|	Расписание.Замена КАК Замена,
		|	Расписание.Совмещение КАК Совмещение,
		|	Расписание.КолвоЧасов КАК КолвоЧасов,
		|	ПРЕДСТАВЛЕНИЕ(Расписание.ЗаменяющийПреподаватель) КАК ЗаменяющийПреподаватель
		|ИЗ
		|	РегистрСведений.Расписание КАК Расписание
		|ГДЕ
		|	Расписание.Предмет = &Предмет
		|	И Расписание.Группа = &Группа
		|	И Расписание.Период МЕЖДУ &Нач И &Кон
		|	И Расписание.Преподаватель = &Преподаватель
		|	И Расписание.ПериодУчебы = &ПериодУчебы";
	
	Запрос.УстановитьПараметр("Группа", Группа);
	Запрос.УстановитьПараметр("Предмет", Предмет);
	Запрос.УстановитьПараметр("Кон", УчебныйГод.КонецГода);
	Запрос.УстановитьПараметр("Нач", УчебныйГод.НачалоГода);
	Запрос.УстановитьПараметр("Преподаватель", ПараметрыСеанса.ТекПользователь);
	Запрос.УстановитьПараметр("ПериодУчебы", УчебныйГод.Период);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СтруктураТем = Новый Структура;
		СтруктураТем.Вставить("Период", ВыборкаДетальныеЗаписи.Период);
		СтруктураТем.Вставить("Тема", ВыборкаДетальныеЗаписи.Тема);
		СтруктураТем.Вставить("Дз", ВыборкаДетальныеЗаписи.Дз);
		СтруктураТем.Вставить("Замена", ВыборкаДетальныеЗаписи.Замена);
		СтруктураТем.Вставить("КолвоЧасов", ВыборкаДетальныеЗаписи.КолвоЧасов);
		СтруктураТем.Вставить("ЗаменяющийПреподаватель", ВыборкаДетальныеЗаписи.ЗаменяющийПреподаватель);
		МассивСтруктур.Добавить(СтруктураТем);
	КонецЦикла;
	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	
	Возврат МассивСтруктур;
	
КонецФункции // ПолучитьСписокТемДляЖурнала()
